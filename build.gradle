apply plugin: 'groovy'
apply plugin: 'maven'

repositories {
    mavenCentral()
    google()
    maven {
        url 'http://spoon.gforge.inria.fr/repositories/releases'
    }
}
configurations {
    releaseJars
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation('fr.inria.gforge.spoon:spoon-core:10.2.0')
    implementation('com.android.tools.build:gradle:4.0.2')

    testImplementation 'junit:junit:4.13.2'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

jar {
    from(configurations.releaseJars.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
    }
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

apply from: 'nexusConfig.gradle'

ext.siteUrl = "https://github.com/SpoonLabs/spoon-gradle-plugin"

ext.commonLicense = {
    name 'GNU Lesser General Public License (LGPL)'
    url 'http://www.gnu.org/licenses/lgpl.html'
}

ext.devs = [{
                id 'GerardPaligot'
                name 'Gerard Paligot'
                email 'gerard.paligot.inria.fr'
            },
            {
                id 'surli'
                name 'Simon Urli'
                email 'simon.urli@inria.fr'
            },
            {
                id 'monperrus'
                name 'Martin Monperrus'
                email 'martin.monperrus@univ-lille1.fr'
            },
            {
                id 'paillardf'
                name 'Florian Paillard'
                email 'paillard.f@gmail.com'
            },
            {
                id 'petitpre'
                name 'Nicolas Petitprez'
                email 'nicolas.petitprez@inria.fr'
            }]


task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.groovy.srcDirs
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories.mavenDeployer {
        pom.project {
            name 'Spoon Gradle Plugin'
            artifactId 'spoon-gradle-plugin'
            packaging 'jar'
            description 'A gradle plugin to run spoon '

            url siteUrl

            scm {
                connection 'scm:git:https://github.com/SpoonLabs/spoon-gradle-plugin.git'
                developerConnection 'scm:git:https://github.com/SpoonLabs/spoon-gradle-plugin'
                url siteUrl
            }

            licenses {
                license commonLicense
            }

            organization {
                name 'INRIA'
                url 'http://www.inria.fr'
            }

            developers {
                devs.collect({ developer it })
            }
        }
    }
}

group = 'fr.inria.gforge.spoon'
version = '1.5-SNAPSHOT'
